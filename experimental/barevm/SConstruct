# I like scons cuz it's Python ;-)
# Import Python's glob instead of scons's since Python's returns a simple
# list of filenames that can be easily manipulated.
#
import glob
DEBUG_FLAG = []

if ARGUMENTS.get('debug', 0):
    DEBUG_FLAG.append('-g')

envcpp = Environment(
            CCFLAGS=['-Wall', '-Wextra', '-pedantic'] + DEBUG_FLAG,
            CPPPATH=['.'],
        )

# Build the 'boblib' library with all Bob source except the main driver,
# and the main driver executable named 'barevm'
#
bobsourcefiles = glob.glob("src/*.cpp")
bobmainfile = "src/main.cpp"
bobsourcefiles.remove(bobmainfile)

envcpp.Library('boblib', source=bobsourcefiles)
envcpp.Program(target='barevm', source=[bobmainfile], 
    LIBS=['boblib'], LIBPATH=['.'])

# Unit-testing with gtest
#
gtestfiles = Glob('tests_unit/gtest/*.cc')
unittests = Glob('tests_unit/*.cpp')

# gtest doesn't like -pedantic... whatever!
envcpptest = Environment(
                CCFLAGS=['-Wall', '-Wextra'] + DEBUG_FLAG,
                CPPPATH=['src/', './tests_unit/'],
            )

envcpptest.Program(target="tests_unit/bob_test_main", 
    source=unittests + gtestfiles, LIBS=['boblib', 'pthread'], LIBPATH=['.'])

